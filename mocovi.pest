
program = {
    SOI ~
    stmts? ~
    EOI
}

stmts = {
    stmt+
}

stmt = {
    compound_stmt |
    (simple_stmt ~ NEWLINE) |
    NEWLINE
}

simple_stmt = {
  assignment |
  return_stmt |
  expr
}

compound_stmt = {
    if_stmt |
    while_stmt |
    func_def
}

return_stmt = {
    "return" ~ expr
}

if_stmt = {
    "if" ~ expr ~ NEWLINE ~
    stmts? ~
    ("else" ~ NEWLINE ~ stmts ~ NEWLINE)? ~
    "end"
}

while_stmt = {
    "while" ~ expr ~ NEWLINE ~
    stmts? ~
    "end"
}

func_def = {
    "def" ~ ident ~ arg_list ~ NEWLINE ~
    stmts? ~
    "end"
}


assignment = {
    ident ~ "=" ~ expr
}

expr = {
    comparison
}

comparison = {
    bool_expr ~ (comp_op ~ bool_expr)*
}

comp_op = {
    "==" | "!=" | ">=" | "<=" | "<" | ">"
}

bool_expr = {
    term ~ (bool_op ~ term)*
}
bool_op = { "and" | "or" }
term = {
    factor ~ (term_op ~ factor)*
}
term_op = { "+" | "-" }
factor = {
    call ~ (factor_op ~ call)*
}
factor_op = { "*" | "/" }

call = {
    ident ~ arg_list |
    primary
}

arg_list = {
    "(" ~ ")" |
    "(" ~ expr ~ ("," ~ expr)* ~ ","? ~ ")"
}

primary = {
    number |
    string |
    boolean |
    nil |
    grouping |
    ident
}

grouping = { "(" ~ expr ~ ")" }

boolean = { "true" | "false" }

nil = { "nil" }

ident = @{
    !(KEYWORD ~ !XID_START) ~
    XID_START ~ XID_CONTINUE*
}

KEYWORD = {
    "do" | "end" | "def" |
    "or" | "and" |
    "if" | "else" | "while" |
    "return"
}

number = @{
    "-"? ~
    integer ~
    ("." ~ mantissa)?
}
integer = { ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT }
mantissa = { ASCII_DIGIT+ }

string = @{
    "\"" ~ (!"\"" ~ ANY)* ~ "\""
}

WHITESPACE = _{ " " }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
